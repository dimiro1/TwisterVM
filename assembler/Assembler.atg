#include "AsmGen.h"
#include <ctype.h>

COMPILER Assembler

	AsmGen *gen;

	CHARACTERS
		letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
		digit = "0123456789".
		cr  = '\r'.
		tab = '\t'.
		lf = '\n'.
		blank = " ".

	TOKENS
		ID = letter {letter | digit | "-" | "_"}.
		NUM = digit {digit} ["." digit {digit}] [("e"|"E" ["-"|"+"]) digit {digit}].

	COMMENTS FROM ";" TO lf

	IGNORE cr + tab + lf

	PRODUCTIONS
      Assembler = HeadSection CodeSection.
		HeadSection = ".begin" "Header" "@size" NUM (. gen->alloc_instruction_array (atoi (coco_string_create_char (t->val))); .) ".end" "Header".

		CodeSection = ".begin" "Code" {CodeInstruction} ".end" "Code".
		CodeInstruction = Push | Add | Sub | Mult | Div | Pop
						| Print | Puts | Nop | Reset | Getop
						| Halt
						.
		Add = "add" (. gen->emit_add (); .).
		Div = "div" (. gen->emit_div (); .).
		Getop = "getop" (. gen->emit_getop (); .).
		Halt = "halt" (. gen->emit_halt (); .).
		Mult = "mult" (. gen->emit_mult (); .).
		Nop = "nop" (. gen->emit_nop (); .).
		Pop = "pop" (. gen->emit_pop (); .).
		Print = "print" (. gen->emit_print (); .).
		Push = "push" NUM (. gen->emit_push (atof (coco_string_create_char (t->val))); .).
		Puts = "puts" (. gen->emit_puts (); .).
		Reset = "reset" (. gen->emit_reset (); .).
		Sub = "sub" (. gen->emit_sub (); .).
END Assembler.
